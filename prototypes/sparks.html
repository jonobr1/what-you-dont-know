<!doctype html>
<html>
  <head>
    <meta name="viewport" content="width=device-width, user-scalable=no">
    <style>
      * {
        margin: 0;
        padding: 0;
      }
      canvas {
        display: block;
        position: absolute;
        top: 0;
        left: 0;
      }
      div.script {
        display: none;
      }
    </style>
  </head>
  <body>
    <div class="scripts">

      <script id="vertex-shader" type="x-shader/x-vertex">

        uniform float time;
        uniform vec3 wind;
        uniform float turbulence;

        attribute float size;
        attribute float life;
        attribute vec3 axisRotation;

        varying float lived;
        varying vec3 particle;
        varying vec3 up;

        float noise( float p ) {
          return sin( p );
        }

        void main() {

          float variation = pow( size, 0.33 );
          float lifetime = life * 1000.0;

          lived = mod( time, lifetime ) / lifetime;

          vec3 destination = position + variation * wind * lived;
          vec3 field = vec3(
            ( wind.y + wind.z ) * noise( destination.y + destination.z ),
            ( wind.x + wind.z ) * noise( destination.x + destination.z ),
            ( wind.x + wind.y ) * noise( destination.x + destination.y )
          );
          field *= turbulence;

          vec3 pos = mix( destination, destination + field, lived );

          particle = pos;
          up = axisRotation;

          vec4 mvPosition = modelViewMatrix * vec4( pos, 1.0 );

          gl_PointSize = size * ( 500.0 / - mvPosition.z );
          gl_Position = projectionMatrix * mvPosition;

        }

      </script>
      <script id="fragment-shader" type="x-shader/x-fragment">

        const float PI = 3.141592653589793;

        uniform float time;
        uniform vec3 specular;
        uniform vec3 diffuse;
        uniform vec3 camera;

        varying float lived;
        varying vec3 particle;
        varying vec3 up;

        mat3 setCamera( in vec3 ro, in vec3 ta, float cr ) {

          vec3 cw = normalize( ta - ro );
          vec3 cp = vec3( sin( cr ), cos( cr ), 0.0 );
          vec3 cu = normalize( cross( cw, cp ) );
          vec3 cv = normalize( cross( cu, cw ) );

          return mat3( cu, cv, cw );

        }

        float sdEllipsoid( in vec3 p, in vec3 r ) {

          float k0 = length( p / r );
          float k1 = length( p / ( r * r ) );

          return k0 * ( k0 - 1.0 ) / k1;

        }

        float sdCapsule( in vec3 p, vec3 a, vec3 b, float r ) {
          vec3 pa = p - a, ba = b - a;
          float h = clamp( dot( pa, ba )/dot( ba, ba ), 0.0, 1.0 );
          return length( pa - ba * h ) - r;
        }

        vec2 map( in vec3 sight ) {

          // Of the object
          vec3 position = vec3( 0.0 );

          // The xyz component radius values of the ellipsoid
          // vec3 radius = vec3( 1.5, 0.1, 1.0 );

          // The vec3 position of the left side of capsule
          vec3 a = vec3( - 1.25, 0.0, 0.0 );
          // The vec3 position of the right side of capsule
          vec3 b = vec3( 1.25, 0.0, 0.0 );
          // Radius of the capsul
          float radius = 0.33;

          // Define position in relation to the camera
          position = sight - position;
          vec3 q = position;

          // // Bend the ellipsoid on the z axis
          // float bend = 0.2;
          // float c = cos( position.z * bend );
          // float s = sin( position.z * bend );
          // mat2  m = mat2( c, - s, s, c );
          // q = vec3( m * position.xy, position.z );
          //
          // // Bend the ellipsoid on the x axis
          // bend = - 0.1;
          // c = cos( q.x * bend );
          // s = sin( q.x * bend );
          // m = mat2( c, - s, s, c );
          // q = vec3( m * q.xy, q.z );

          vec3 worldPosition = q;

          // float sdf = sdEllipsoid( worldPosition, radius );
          float sdf = sdCapsule( worldPosition, a, b, radius );
          vec2 res = vec2( sdf, 0.0 );

          return res;

        }

        vec2 castRay( in vec3 ro, in vec3 rd ) {

          // Near / Far Clipping Plane
          float tmin = 1.0;
          float tmax = 50.0;

          float t = tmin;
          float m = - 1.0;

          // TODO: Why does it need so many iterations
          // to march correctly?
          for( int i = 0; i < 128; i++ ) {

            float precis = 0.0004 * t;
            vec2 res = map( ro + rd * t );

            // Means no intersection
            // and no possibility of checking again
            // so stop the Ray Marching
            if ( res.x < precis || t > tmax ) break;

              t += res.x;
              m = res.y; // Identify which shape was intersected via a float

            }

            if( t > tmax ) m =- 1.0;
            return vec2( t, m );

        }

        // https://www.geeks3d.com/20141201/how-to-rotate-a-vertex-by-a-quaternion-in-glsl/
        vec4 quat_from_axis_angle( vec3 axis, float angle ) {
          float half_angle = angle * 0.5;
          float s = sin( half_angle );
          return vec4( axis.xyz * s, cos( half_angle ) );
        }

        // https://www.geeks3d.com/20141201/how-to-rotate-a-vertex-by-a-quaternion-in-glsl/
        vec3 rotate( vec3 position, vec3 axis, float angle ) {
          vec4 q = quat_from_axis_angle( axis, angle );
          vec3 v = position.xyz;
          return v + 2.0 * cross( q.xyz, cross( q.xyz, v ) + q.w * v );
        }

        void main() {

          float theta = ( time / 100.0 ) * up.x * up.y * up.z;
          float azimuth = 1.0;

          vec2 uv = 2.0 * vec2( gl_PointCoord ) - 1.0;
          vec3 ro = normalize( ( camera - particle ) );

          ro = rotate( ro, up, theta );
          ro *= 10.0 / length( ro );

          vec3 ta = vec3( 0.0, 0.0, 0.0 );
          mat3 ca = setCamera( ro, ta, 0.0 );

          vec3 rd = ca * normalize( vec3( uv.xy, 5.0 ) );
          vec2 res = castRay( ro, rd );

          float t = res.x;
          float m = res.y;
          float alpha = step( 0.0, m );

          vec3 texel = specular;

          gl_FragColor = vec4( texel, alpha * pow( sin( lived * PI ), 1.0 ) );

        }

      </script>

      <script src="../release/third-party/three.js"></script>
      <script src="../release/third-party/TrackballControls.js"></script>
      <!-- <script src="../release/src/interaction.js"></script> -->
      <script src="../release/third-party/WebVR.js"></script>
      <script>

        var renderer = new THREE.WebGLRenderer( { antialias: true } );
        var scene = new THREE.Scene();
        var camera = new THREE.PerspectiveCamera( 75 );
        var controls;
        var loader = new THREE.TextureLoader();

        var cube = new THREE.Mesh(
          new THREE.BoxBufferGeometry( 25, 25, 25 ),
          new THREE.MeshNormalMaterial()
        );

        var amount = 10000;
        var radius = 50;

        var geometry = new THREE.BufferGeometry();
        var positions = [];
        var sizes = [];
        var lifeTimes = [];
        var rotations = [];

        for ( var i = 0; i < amount; i++ ) {

          positions.push( ( 2 * Math.random() - 1 ) * radius );
          positions.push( ( 2 * Math.random() - 1 ) * radius );
          positions.push( ( 2 * Math.random() - 1 ) * radius );

          sizes.push( Math.random() );
          lifeTimes.push( Math.random() * 19 + 1 );

          rotations.push( ( 2 * Math.random() - 1 ) );
          rotations.push( ( 2 * Math.random() - 1 ) );
          rotations.push( ( 2 * Math.random() - 1 ) );

        }

        geometry.addAttribute( 'position', new THREE.Float32BufferAttribute( positions, 3 ) );
        geometry.addAttribute( 'size', new THREE.Float32BufferAttribute( sizes, 1 ) );
        geometry.addAttribute( 'life', new THREE.Float32BufferAttribute( lifeTimes, 1 ) );
        geometry.addAttribute( 'axisRotation', new THREE.Float32BufferAttribute( rotations, 3 ) );

        var material = new THREE.ShaderMaterial({
          uniforms: {
            specular: { type: 'c', value: new THREE.Color( 1.0, 1.0, 1.0 ) },
            diffuse: { type: 'c', value: new THREE.Color( 0.0, 0.0, 0.0 ) },
            wind: { type: 'v3', value: new THREE.Vector3() },
            time: { type: 'f', value: 0 },
            turbulence: { type: 'f', value: 0 },
            camera: { type: 'v3', value: camera.position }
          },
          vertexShader: document.body.querySelector( '#vertex-shader' ).textContent,
          fragmentShader: document.body.querySelector( '#fragment-shader' ).textContent,
          transparent: true,
          // depthTest: false
          depthWrite: false
        });

        var debris = new THREE.Points( geometry, material );

        scene.add( debris );
        scene.add( cube );

        setup();

        function setup() {

          scene.add( camera );
          camera.position.z = 1;

          renderer.setClearColor( 0xaaaaaa );
          // renderer.vr.enabled = true;

          document.body.appendChild( WEBVR.createButton( renderer ) );

          document.body.appendChild( renderer.domElement );

          window.addEventListener( 'resize', resize, false );
          resize();

          controls = new THREE.TrackballControls( camera, renderer.domElement );
          controls.rotateSpeed = 5.0;
          controls.zoomSpeed = 2.2;
          controls.panSpeed = 1;
          controls.dynamicDampingFactor = 0.3;

          debris.material.uniforms.turbulence.value = 0.05;
          debris.material.uniforms.wind.value.y = - 10;
          // debris.material.uniforms.wind.value.x = 20;

          renderer.setAnimationLoop( animate );

        }

        function resize() {

          var width = window.innerWidth;
          var height = window.innerHeight;

          renderer.setSize( width, height );

          camera.aspect = width / height;
          camera.updateProjectionMatrix();

        }

        function animate( time ) {

          controls.update();
          debris.material.uniforms.time.value = time;
          // debris.material.uniforms.turbulence.value = 10 * Math.sin( time / 1000 );
          renderer.render( scene, camera );

        }

      </script>
    </div>
  </body>
</html>
